---
layout: post
title:  "sdBox 분석"
date:   2014-10-14 16:28:00
categories: jekyll update
---

[Inigo Quilez 의 distance functions](http://iquilezles.org/www/articles/distfunctions/distfunctions.htm) 가운데 'Box-signed' 인 sdBox 함수를 분석해 보겠습니다.

![sdBox]({{site.url}}/assets/sdbox01.png)

[glslsandbox](http://glslsandbox.com/) 에서 'Create new effect!' 를 눌러 아래 코드를 입력해 가면서 과정을 확인할 수 있습니다.

## 원본 그대로 따라 쓰기

일단 뭔지 모를 때는 노트에 연필로 필기해 보면서 어떤 의미인지 추측해봐도 좋고, 타이핑하면서도 추측을 해 봅니다.

```glsl
float sdBox(vec3 p, vec3 b) {
    vec3 d = abs(p) - b;
    return min(max(d.x, max(d.y, d.z)), 0.0) + length(max(d, 0.0));
}
```

## 차원 낮춰보기 : vec3 -> vec2

vec3 는 x,y,z 로 표현되는 3차원 좌표계상의 점이라 머리 속으로 어떤 이미지를 떠올리기가 어려워서 2차원인 vec2 로 차원을 낮춰 생각해 보겠습니다.

```glsl
float sdBox(vec2 p, vec2 b) {
    vec2 d = abs(p) - b;
    return min(max(d.x, d.y), 0.0) + length(max(d, 0.0));
}
```

## 결론 먼저 적어보면
위 함수를 보고 바로 알게 된 것은 아니고, 주욱 쪼개서 분석해 나가다 보니까 결론적으로 이해하게 된 내용 입니다.

* `length(max(d, 0.0)` : 0.5 안쪽 영역의 값이 0, 바깥쪽 값은 양수
* `min(max(d.x, d.y), 0.0)` : 0.5 안쪽 영역의 값은 음수, 바깥쪽 값은 0

이해하기 위해 하나하나를 더 쪼개고 시각화 해보겠습니다.

## length(max(d, 0.0)) 쪼개서 이해해보기

* `length(max(d, 0.0))` 을 이해하려면, `d`를 먼저 이해해야 합니다.
* `d` 를 이해하려면, `abs(p)` 를 이해해야 하구요.
* `abs(p)`를 이해하기 위해 다음과 같은 것은 어떤 형태인지 살펴보겠습니다.
  * `abs(p.x)`
  * `abs(p.y)`
  * `length(abs(p))`

```glsl
void main( void ) {
    vec2 p = (2.0*gl_FragCoord.xy - resolution.xy)/resolution.y;
    vec2 d = abs(p);
    float c = length(d);//d.x; //d.y;
    gl_FragColor = vec4(vec3(c), 1.0);
}
```
`length(d)` 대신 `d.x` 혹은 `d.y` 등을 대입해서 어떤 모양이 되는 지도 확인해 봅시다.

### abs(x)-0.5

[geogbra](http://www.geogebra.org/), [desmos](https://www.desmos.com/) 등을 이용해서 함수의 결과 값이 어떤 범위를 갖는 지 확인해 볼 수 있습니다.

![desmosS01]({{site.url}}/assets/sdbox02.png)

```glsl
void main(void) {
    vec2 p = (2.0*gl_FragCoord.xy - resolution.xy)/resolution.y;
    vec2 b = vec2(0.5, 0.5);
    vec2 d = abs(p) - b;
    float c = d.x;
    gl_FragColor = vec4(vec3(c), 1.0);
}
```

![glslBoxS01]({{site.url}}/assets/sdbox03.png)

### length(abs(x)-0.5)

![desmosS02]({{site.url}}/assets/sdbox04.png)

```glsl
void main(void) {
    vec2 p = (2.0*gl_FragCoord.xy - resolution.xy)/resolution.y;
    vec2 b = vec2(0.5, 0.5);
    vec2 d = abs(p) - b;
    float c = length(d.x);
    gl_FragColor = vec4(vec(c), 1.0);
}
```

![glslBoxS02]({{site.url}}/assets/sdbox05.png)

### max(abs(x)-0.5, 0.0)

![desmosS03]({{site.url}}/assets/sdbox06.png)

```glsl
void main(void) {
    vec2 p = (2.0*gl_FragCoord.xy - resolution.xy)/resolution.y;
    vec2 b = vec2(0.5, 0.5);
    vec2 d = abs(p) - b;
    d = max(d, 0.0);
    float c = d.x;
    gl_FragColor = vec4(vec3(c), 1.0);
}
```

![glslBoxS03]({{site.url}}/assets/sdbox07.png)

### length(max(abs(p)-b, 0.0))

```glsl
void main(void) {
    vec2 p = (2.0*gl_FragCoord.xy - resolution.xy)/resolution.y;
    vec2 b = vec2(0.5, 0.5);
    vec2 d = abs(p) - b;
    d = max(d, 0.0);
    float c = length(d);
    gl_FragColor = vec4(vec3(c), 1.0);
}
```

![glslBoxS04]({{site.url}}/assets/sdbox08.png)

## min(max(dx, dy), 0.0) 쪼개서 이해해 보기

### max(dx, dy)

```glsl
void main(void) {
    vec2 p = (2.0*gl_FragCoord.xy - resolution.xy)/resolution.y;
    vec2 b = vec2(0.5, 0.5);
    vec2 d = abs(p) - b;
    float c = max(d.x, d.y);
    gl_FragColor = vec4(vec3(c), 1.0);
}
```

![glslBoxS05]({{site.url}}/assets/sdbox09.png)

### min(max(d.x, d.y), 0.0)

```glsl
void main(void) {
    vec2 p = (2.0*gl_FragCoord.xy - resolution.xy)/resolution.y;
    vec2 b = vec2(0.5, 0.5);
    vec2 d = abs(p) - b;
    float c = 0.5 + min(max(d.x, d.y), 0.0);
    gl_FragColor = vec4(vec3(c), 1.0);
}
```

![glslBoxS06]({{site.url}}/assets/sdbox10.png)

## 쪼갰던 것을 다시 하나씩 붙여보기

```glsl
void main(void) {
    vec2 p = (2.0*gl_FragCoord.xy - resolution.xy)/resolution.y;
    vec2 b = vec2(0.5, 0.5);
    vec2 d = abs(p) - b;
    float box = -min(max(d.x, d.y), 0.0) + length(max(d, 0.0));
    gl_FragColor = vec4(vec3(box), 1.0);
}
```

* 그냥 min(max(d.x, d.y), 0.0) 을 하면 0.5 안쪽 영역이 음수라 까맣게 나오므로 앞에 -를 붙여 반전시킵니다.

![glslBoxS07]({{site.url}}/assets/sdbox11.png)
